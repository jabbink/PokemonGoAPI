group 'ink.abb.pogo'
version '0.0.1'

buildscript {
    ext.kotlin_version = '1.0.3'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.7.7'
        classpath files("src/main/java")
        classpath files("src/main/kotlin")
    }
}

apply plugin: 'kotlin'
apply plugin: 'com.google.protobuf'

sourceCompatibility = 1.8

File apiExtensionsOutputDir = file("src/generated/kotlin")

sourceSets {
    main {
        proto {
            // Need to use custom dir cause Gradle doesn't like us otherwise :(
            srcDir 'lib/POGOProtos/src'
            include '**/*.proto'
        }
        java {
            srcDir "$buildDir/generated/source/proto/main/java"
            srcDir "$buildDir/generated/source/java/main"
        }
        kotlin {
            srcDir 'src/main/kotlin'
        }
    }
}

// Remove all .proto definition from the final build
processResources {
    exclude('lib/POGOProtos/')
}

protobuf {
    // Configure the protoc executable
    protoc {
        // Download from repositories
        artifact = 'com.google.protobuf:protoc:3.0.0-beta-3'
    }
}
task(generateApi, dependsOn: classes, type: JavaExec) {
    main = 'ink.abb.pogo.api.GenerateApiKt'
    classpath = sourceSets.main.runtimeClasspath
    args "$buildDir/generated/source/java/main"
}

defaultTasks 'generateApi'

repositories {
    mavenCentral()
}

dependencies {
    compile gradleApi()

    compile 'com.google.protobuf:protobuf-java:3.0.0-beta-3'
    compile group: 'com.sun.codemodel', name: 'codemodel', version: '2.6'

    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    testCompile group: 'junit', name: 'junit', version: '4.11'
}
